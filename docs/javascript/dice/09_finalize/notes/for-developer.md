# 開発者向け

## 各役について

パターン数は **6 面ダイスを 5 個振る**ことより、`6**5 = 7776` となる。その上でダイスパターンは、下表のようになる。

|ダイスパターン|数|
|:---:|:---:|
|AAAAA|6|
|AAAAB|150|
|AAABB|300|
|AAABC|1200|
|AABBC|1800|
|AABCD|3600|
|ABCDE|720|

上記よりローワーセクションに属する役について、そのパターン数と確率（振り直しを考慮しない、１回のロールで成立するケース）は下図および下表のようにまとめられる。  
なお振り直しを考慮した場合、この確率は必ずしも成立難度と一致はしないことには注意。

![下部の役関係をベン図で示したもの、全パターン 7776 の内、スリーダイス(1656) の中にフルハウス(300) とフォーダイス(156) が含まれ、フォーダイス内にフネ(6)が含まれる。それとは別にショートストレート(840) 内にロングストレート(240) が含まれている](./images/venn-diagram-to-under-section.png)

|役名|パターン数|確率(※1)|
|:---|---:|---:|
|FUNE|6|0.0772%|
|FourDice|156|2.0062%|
|LongStraight|240|3.0864%|
|FullHouse|300|3.8580%|
|ShortStraight|840|10.8025%|
|ThreeDice|1656|21.2963%|

※1: 小数第５位を四捨五入した、おおよその値

### Google SpreadSheet を使った検算

1行目は行見出しとして利用するため、2行目以降に関数を使っていく。基本的には下記をそれぞれ 7777行目まで拡張することになる。1行目が見出しなので、実質データセット数は 7776 であり、これは「6面ダイスを5個振るパターン数 `6 ** 5 = 7776`」と一致する。

#### ダイスパターン（A列）

|セル|数式|
|:---:|:---|
|A2|`=BASE(ROW()-2,6,5)`|

ここでは、**6進数で5桁までの一覧**を並べている。A7778 に拡張すると `100000` と6桁になるが、これは必要となるデータからは外れる。

#### ダイス分割（C〜G列）

|セル|数式|
|:---:|:---|
|B2〜G2|`=MID($A2,COLUMN()-2,1)+1`|

A列のダイスパターンは 6 進数であるため、0〜5までの数値で数を表す。ここでは**実際のダイス 1 〜 6 に合わせる意図**も合わせて、各桁をセルに分割している。

なお、`COLUMN()-2` の部分は固定値でも問題ない。ここではわざわざベタ打ちするのが面倒だったため、列を使った連番を生成している。

#### ダイスソート（I〜M列）

|セル|数式|
|:---:|:---|
|I2|`=SMALL($C2:$G2,1)`|
|J2|`=SMALL($C2:$G2,2)`|
|K2|`=SMALL($C2:$G2,3)`|
|L2|`=SMALL($C2:$G2,4)`|
|M2|`=SMALL($C2:$G2,5)`|

C〜G列でダイスをセルに分割したので、次はそれを管理しやすいよう昇順にソートしている。

`COLUMN()` を使わず固定値で 1〜5 を指定しているのは、A列から離れすぎているため。数値をいじれば「ダイス分割」と同じようできるが、列の変更の際の確認の手間やミスの恐れもある以上、推奨はしない。

#### 役判定（O〜T列）

|役名|セル|数式|
|:---|:---:|:---|
|FUNE|O2|`=IF(AND(I2=J2,J2=K2,K2=L2,L2=M2),1,0)`|
|FullHouse|P2|`=IF(OR(AND(I2=J2,J2=K2,K2<>L2,L2=M2),AND(I2=J2,J2<>K2,K2=L2,L2=M2)),1,0)`|
|ShortStraight|Q2|`=IF(OR(AND(I2+1=J2,J2+1=K2,K2+1=L2),AND(J2+1=K2,K2+1=L2,L2+1=M2)),1,0)`|
|LongStraight|R2|`=IF(AND(I2+1=J2,J2+1=K2,K2+1=L2,L2+1=M2),1,0)`|
|ThreeDice|S2|`=IF(OR(AND(I2=J2,J2=K2),AND(J2=K2,K2=L2),AND(K2=L2,L2=M2)),1,0)`|
|FourDice|T2|`=IF(OR(AND(I2=J2,J2=K2,K2=L2),AND(J2=K2,K2=L2,L2=M2)),1,0)`|

各役の判定が行われている。ダイスは昇順にソートされている以上、必要な判定式は単純なものになっている。`IF`文を使い、条件を満たせば `1`、そうでなければ `0` が出力される。

- FUNE: 隣り合うセル値が全部同じか
- FullHouse: 「3種→2種」または「2種→3種」で同じか（※種の切り替わりの所は違う値である必要）
- ShortStraight: 1番目〜4番目まで、あるいは2番目〜5番目までで、数値が連番になっているか
- LongStraight: 最初から最後まで、数値は連番になっているか
- ThreeDice: 最初〜3番目、または2〜4番目、または3番目〜最後のどこかで、同じ数値になっているか
- FourDice: 最初〜4番目、または2〜最後のどちらかで、同じ数値になっているか

後はフィルタリングするなり、`SUM`関数を使うなりすれば、必要なパターンとその数を見つけられる。
